plugins {
    id 'war'  
}
description = "Accelerator task calendar"
group 'org.jlab'
version = '3.11.2'

ext {
    releaseDate = 'Mar 9 2023'
}
tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    // Setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
                    'org.apache.poi:poi:5.2.3',
                    'org.apache.poi:poi-ooxml:5.2.3'

    // Wildfly 26.1.3.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2',
     	            'org.hibernate:hibernate-core:5.3.28.Final',
                    'org.hibernate:hibernate-envers:5.3.28.Final'

    testImplementation 'junit:junit:4.13.2'
}
test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}
war {
    archiveFileName='calendar.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}